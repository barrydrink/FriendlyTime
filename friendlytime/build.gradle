apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/bellabling/friendly-time'      // Homepage URL of the library
def gitUrl = 'https://github.com/bellabling/friendly-time.git'   // Git repository URL
group = 'com.bellabling'
version = '0.0.1'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/classes.jar/')
    into('build')
    include('classes.jar')
    rename('classes.jar', 'friendlytime.jar')
}

task deleteJar(type: Delete) {
    delete 'build/friendlytime.jar'
}
createJar.dependsOn(deleteJar, build)

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = System.env.BINTRAY_USER_PERSONAL
    key = System.env.BINTRAY_APIKEY_PERSONAL

//    configurations = ['archives']

    filesSpec {
        from 'build/friendlytime.jar'
        into '.'
    }
    pkg {
        repo = 'bellabling-maven'
        name = 'friendlytime'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        publicDownloadNumbers = true
        publish = false
        version {
            name = version
        }
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
